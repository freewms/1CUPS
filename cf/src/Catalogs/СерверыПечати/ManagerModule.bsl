// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
	
// Получить описание подключения сервера печати.
// 
// Параметры:
//  СерверПечати - СправочникСсылка.СерверыПечати
// 
// Возвращаемое значение:
//  Структура - Получить описание подкючения сервера печати:
// * Сервер - Строка -
// * Порт - Число -
// * Пользователь - Строка -
// * Пароль - Строка -
// * ЗащищенноеСоединение - Булево -
// * Таймаут - Число -
// * Прокси - см. ОписаниеИнтернетПрокси 
Функция	ПолучитьОписаниеПодключенияСервераПечати(СерверПечати) Экспорт
	
	ОписаниеСоединения = Новый Структура;
	ОписаниеСоединения.Вставить("Сервер","");
	ОписаниеСоединения.Вставить("Порт",0);
	ОписаниеСоединения.Вставить("Пользователь","");
	ОписаниеСоединения.Вставить("Пароль","");
	ОписаниеСоединения.Вставить("ЗащищенноеСоединение",Ложь);
	ОписаниеСоединения.Вставить("Таймаут",0);
	ОписаниеСоединения.Вставить("Прокси", ОписаниеИнтернетПрокси());
	ОписаниеСоединения.Вставить("ИспользоватьПрокси", Ложь);
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	СерверыПечати.Наименование,
	|	СерверыПечати.Сервер,
	|	СерверыПечати.Порт,
	|	СерверыПечати.ЗащищенноеСоединение,
	|	СерверыПечати.Пользователь,
	|	СерверыПечати.Пароль,
	|	СерверыПечати.Таймаут,
	|	СерверыПечати.ПроксиСервер,
	|	СерверыПечати.ПроксиПорт,
	|	СерверыПечати.ПроксиПользователь,
	|	СерверыПечати.ПроксиПароль,
	|	СерверыПечати.ИспользоватьПрокси
	|ИЗ
	|	Справочник.СерверыПечати КАК СерверыПечати
	|ГДЕ
	|	СерверыПечати.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",СерверПечати);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ОписаниеСоединения,Выборка);
		Если Выборка.ИспользоватьПрокси Тогда
			ОписаниеСоединения.Прокси.Сервер = Выборка.ПроксиСервер;
			ОписаниеСоединения.Прокси.Порт = Выборка.ПроксиПорт;
			ОписаниеСоединения.Прокси.Пользователь = Выборка.ПроксиПользователь;
			ОписаниеСоединения.Прокси.Пароль = Выборка.ПроксиПароль;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОписаниеСоединения;
				
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Описание интернет прокси.
// 
// Возвращаемое значение:
//  Структура - Описание интернет прокси:
// * Сервер - Строка -
// * Порт - Число -
// * Пользователь - Строка -
// * Пароль - Строка -
Функция ОписаниеИнтернетПрокси()
	
	ИнтернетПрокси = Новый Структура;
	ИнтернетПрокси.Вставить("Сервер", "");
	ИнтернетПрокси.Вставить("Порт", 0);
	ИнтернетПрокси.Вставить("Пользователь", "");
	ИнтернетПрокси.Вставить("Пароль", "");
	
	Возврат ИнтернетПрокси;
	
КонецФункции

#КонецОбласти

#КонецЕсли
