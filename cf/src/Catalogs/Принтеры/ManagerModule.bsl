// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Пометить на удаление.
// 
// Параметры:
//  Ссылка - СправочникСсылка.Принтеры
Процедура ПометитьНаУдаление(Ссылка) Экспорт
	
	СправочникОбъект = Ссылка.ПолучитьОбъект();
	СправочникОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

// Создать новый элемент справочника по описанию.
// 
// Параметры:
//  СерверПечати - СправочникСсылка.СерверыПечати
//  ИмяПринтера - Строка - Имя принтера на сервере печати
//  ФорматБумаги - СправочникСсылка.ФорматыБумаги - Формат бумаги
//  ОриентацияСтраницы - ПеречислениеСсылка.ОриентацияСтраницы - Ориентация страницы
Процедура СоздатьНовыйЭлементПоОписанию(СерверПечати, 
								 ИмяПринтера, 
								 ФорматБумаги = Неопределено, 
								 ОриентацияСтраницы=Неопределено) Экспорт
	
	СправочникОбъект = Справочники.Принтеры.СоздатьЭлемент();
	СправочникОбъект.ИмяНаСервереПечати = ИмяПринтера;
	СправочникОбъект.Наименование = ИмяПринтера;
	СправочникОбъект.СерверПечати = СерверПечати;
	ПараметрыПоУмолчанию = СервисПечатиПовтИсп.ПолучитьПараметрыБумагиПоУмолчанию();
	СправочникОбъект.ФорматБумаги = ПараметрыПоУмолчанию.Формат;
	СправочникОбъект.ОриентацияСтраницы = ПараметрыПоУмолчанию.Ориентация;
	СправочникОбъект.Записать();		

КонецПроцедуры

// Проверка является ли наименование элемента соответствующим шаблону наименования.
// Фактически - проверяет является ли наименование автоматически сформированным.
// 
// Параметры:
//  Принтер - СправочникСсылка.Принтеры - Принтер
// 
// Возвращаемое значение:
//  Булево - Наименование соответствует шаблону
Функция НаименованиеСоответствуетШаблону(Принтер) Экспорт
	
	ТекущееНаименование = "";
	ТекущееНаименованиеПоШаблону = "";

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Принтеры.Наименование,
	|	Принтеры.ИмяНаСервереПечати КАК ИмяПринтера
	|ИЗ
	|	Справочник.Принтеры КАК Принтеры
	|ГДЕ
	|	Принтеры.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Принтер);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекущееНаименование = Строка(Выборка.Наименование);
		ДанныеЗаполнения = Новый Структура();
		ДанныеЗаполнения.Вставить("ИмяПринтера",Выборка.ИмяПринтера);
		ТекущееНаименованиеПоШаблону = ПолучитьНаименованиеПоШаблону(ДанныеЗаполнения);
	КонецЕсли;
			
	Возврат ТекущееНаименование = ТекущееНаименованиеПоШаблону;
	
КонецФункции

// Получить наименование по шаблону.
// 
// Параметры:
//  ДанныеЗаполнения - Структура - Данные заполнения:
//   * ИмяПринтера - Строка - Имя принтера на сервере печати
// 
// Возвращаемое значение:
//  Строка - Наименование элемента справочника по шаблону
Функция ПолучитьНаименованиеПоШаблону(ДанныеЗаполнения) Экспорт

	Возврат Строка(ДанныеЗаполнения.ИмяПринтера);

КонецФункции 

#КонецОбласти

#КонецЕсли