// @strict-types

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьСостояниеПринтера", 0.1, Истина);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНаименование(Команда)
	
	ЗаполнитьНаименованиеНаСервере();
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаименованиеНаСервере()

	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ИмяПринтера", Объект.ИмяНаСервереПечати);
	Наименование = Справочники.Принтеры.ПолучитьНаименованиеПоШаблону(ДанныеЗаполнения);
	Объект.Наименование = Наименование;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеПринтера()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СерверПечати = Объект.СерверПечати;
	ИмяПринтера = Объект.ИмяНаСервереПечати;
		
	Результат = ПолучитьСостояниеПринтера(СерверПечати, ИмяПринтера);
	Если Результат = Неопределено Тогда
		СостояниеПринтера = ПредставлениеКодаСостоянияПринтера(Неопределено);
		ОписаниеСостояния = "Не удалось получить информацию о состоянии принтера";
		Возврат;
	КонецЕсли;
	
	СостояниеПринтера = ПредставлениеКодаСостоянияПринтера(Результат[0]["data"]["printer-state"]);
	ОписаниеСостояния = Строка(Результат[0]["data"]["printer-state-message"]);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеПринтера(СерверПечати, ИмяПринтера)
	
	Возврат СервисПечатиСервер.ПолучитьСостояниеПринтера(СерверПечати, ИмяПринтера);
	 
КонецФункции

&НаКлиенте
Функция ПредставлениеКодаСостоянияПринтера(КодСостояния)
	
	//RFC8011 - printer-state
	//Дополнительно введены:
	// 0 - при возникновении любых ошибок на сервере печати
	// 1 - при успешной отправке заданий на принтер
	
	Состояние = "";
	Если КодСостояния = 0 Тогда
		Состояние = "Ошибка";
	ИначеЕсли КодСостояния = 1 Тогда
		Состояние = "Задание принято";
	ИначеЕсли КодСостояния = 3 Тогда
		Состояние = "Ожидает";
	ИначеЕсли КодСостояния = 4 Тогда
		Состояние = "Печатает";
	ИначеЕсли КодСостояния = 5 Тогда
		Состояние = "Остановлен";
	Иначе
		Состояние = "Неизвестное состояние";
	КонецЕсли;
		
	Возврат Состояние;

КонецФункции	
	

#КонецОбласти
